PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX api4kp: <https://www.omg.org/spec/API4KP/api4kp/>
PREFIX api4kp-rel: <https://www.omg.org/spec/API4KP/api4kp-rel/>

SELECT ?s ?p ?o
WHERE
{
  # imports chain
  {
    ?root api4kp-rel:imports+ ?o .
    BIND( api4kp-rel:imports AS ?p )
  	BIND( ?root AS ?s )
  }
  UNION
  {
    ?root api4kp-rel:imports* ?s .
    ?s api4kp-rel:imports ?o .
  	BIND( api4kp-rel:imports AS ?p )
  }
  UNION
  # additional dependencies of imports closure
  {
    ?root api4kp-rel:imports* ?s .
    ?s api4kp:dependsOn ?o .
    BIND( api4kp:dependsOn AS ?p )
  }
  UNION
  # non-trivial asset types
  {
    ?root api4kp:dependsOn* ?s .
    ?s rdf:type ?o .
    ?o rdfs:subClassOf+ api4kp:KnowledgeAsset .
  	BIND( rdf:type AS ?p )
  }

}
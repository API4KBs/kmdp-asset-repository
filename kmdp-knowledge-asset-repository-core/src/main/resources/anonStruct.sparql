PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX api4kp: <https://www.omg.org/spec/API4KP/api4kp/>
PREFIX api4kp-rel: <https://www.omg.org/spec/API4KP/api4kp-rel/>

SELECT ?s ?p ?o
WHERE
{
  # transitive closure of imports
  {
    ?root api4kp-rel:imports+ ?o .
    BIND( api4kp-rel:imports AS ?p )
    BIND( ?root AS ?s )
  }
  UNION
  # imports chain
  {
    ?s api4kp-rel:imports ?o .
  	BIND( api4kp-rel:imports AS ?p )
  	FILTER EXISTS { ?root api4kp-rel:imports* ?s . }
  }
  UNION
  # additional dependencies of imports closure
  {
    ?s ?x ?o .
  	?x rdfs:subPropertyOf* api4kp:dependsOn .
  	BIND( api4kp:dependsOn AS ?p )
  	FILTER EXISTS { ?root api4kp-rel:imports* ?s . }
  }
  UNION
  # non-trivial asset types
  {
    ?s rdf:type ?o .
    ?o rdfs:subClassOf+ api4kp:KnowledgeAsset .
  	FILTER EXISTS { ?root api4kp:dependsOn* ?s . }
  	BIND( rdf:type AS ?p )
  }

}